buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jmailen.gradle:kotlinter-gradle:$kotlinLinterVer"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVer"
    id 'org.jmailen.kotlinter' version "$kotlinLinterVer"
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'com.github.johnrengelman.shadow'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    group = 'jp.co.soramitsu.iroha-java'
    version = 'git rev-parse --short HEAD'.execute().text.trim()

    test {
        useJUnitPlatform()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = 1.8
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = 1.8
        }
    }

    kotlinter {
        ignoreFailures = false
        indentSize = 4
        reporters = ['checkstyle', 'plain']
        experimentalRules = false
        disabledRules = []
    }

    shadowJar {
        zip64 = true
        archiveFileName = "${archiveBaseName.get()}-${archiveVersion.get()}.jar"
    }
}

task allShadowJars {
    subprojects { pr -> allShadowJars.dependsOn("${pr.path}:shadowJar") }
}

check {
    dependsOn "installKotlinterPrePushHook"
}
